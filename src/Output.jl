## Output functions

"""
    $(TYPEDSIGNATURES)

Pad string `s` to predefined width or keep unchanged if `width ≤ length(str)`.
Setting `padfront=false` inserts padding after the string (left aligned)
instead of in front. 
"""
function padstring(s::String, width::Int; padfront::Bool=true)
  str_length = length(s)
  if width <= str_length
    str_out =  s
  else
    pad_str = repeat(" ", width - str_length)
    str_out = padfront ? pad_str * s : s * pad_str
  end
  return str_out
end

"""
    $(TYPEDSIGNATURES)

Print TFPV (candidates) to terminal or return LaTeXString that can be included in
tex file.
"""
function print_tfpv(idx::Vector{Vector{Bool}}, prob::ReductionProblem; latex=false)
  if latex
    parameters = [latexify(πᵢ; env=:raw) for πᵢ in string.(prob.π)]
    m = length(parameters)
    str = "\\begin{array}{r$(repeat('c',length(string(prob.π))))} i & $(join(parameters, "^* & ")) \\\\ \n"
    for i in eachindex(idx)
      str *= "$i & " * join([idx[i][k] ? parameters[k] : "\\cdot" for k = 1:m], " & ") * " \\\\ \n"
    end
    str *= "\\end{array}"
    return latexstring(str)
  else
    subscripts = ["₀","₁","₂","₃","₄","₅","₆","₇","₈","₉"]
    numbers = string.(0:9)
    max_width = ndigits(length(idx)) 
    field_widths = [length(πᵢ) for πᵢ in string.(prob.π)]
    str = "π̃ $(repeat(" ", max_width-1)) = ($(join(string.(prob.π), ", ")))\n"
    str *= repeat("_", length(str)-2) * "\n"
    for i = 1:length(idx)
      idx_fast = idx[i]
      str *= "π" *
        join([subscripts[string(n) .== numbers][1] for n in string(i)], "") *
        "$(repeat(" ", max_width - ndigits(i))) = (" * 
        join(padstring.([idx_fast[k] ? string(prob.π[k]) : "0" for k = 1:length(idx_fast)], field_widths), ", ") *")\n"
    end
    print(str)
    return nothing
  end
end

"""
    $(TYPEDSIGNATURES)

Print generators of ideals in corresponding to irreducible components of varieties
for TFPV candidates to terminal (`V` as returned by `filter_dimension` or
`tfpv_candidates`).

See also: [`filter_dimension`](@ref), [`tfpv_candidates`](@ref)
"""
function print_varieties(V, prob::ReductionProblem; latex=false)
  if latex
    str_latex = "\\begin{array}{rl} i & \\mathcal{V}(f(\\cdot, \\tilde{\\pi_i})) \\\\ \n"
    for i = 1:length(V)
      Q = V[i]
      str = ["\\mathcal{V}(" * join(latexify.(string.(Q[i]); env=:raw, cdot=false), ", ") * ")" for i in eachindex(Q)]
      str_out = join(str, " \\cup ")
      str_latex *= "$i & " * str_out * "\\\\ \n"
    end
    str_latex *= "\\end{array}"
    return latexstring(str_latex)
  else
    subscripts = ["₀","₁","₂","₃","₄","₅","₆","₇","₈","₉"]
    numbers = string.(0:9)
    max_width = ndigits(length(V)) 
    pad = "$(repeat(" ", max_width + 4))"
    for i = 1:length(V)
      V_str = ["[" * replace(join(string.(Y), ", ")) * "]" for Y∈V[i]]
      str = "V" * 
        join([subscripts[string(n) .== numbers][1] for n in string(i)], "") * 
        "$(repeat(" ", max_width - ndigits(i))) : " * 
        join(V_str, "\n" * pad)
      println(str)
    end
    return nothing
  end
end

"""
    $(TYPEDSIGNATURES)

Print slow-fast separations that are TFPVs and generators for the corresponding
irreducible components of `V(f⁰)` together with their Krull dimension.

### Arguments 
- `idx`: Boolean indices defining all TFPVs `π⁺` that are slow-fast separations (0: slow, 1: fast).
- `V`: Generators for the irreducible component of the affine varietiy `V(f(⋅,π⁺))` for each slow-fast separation.
- `dim_V`: Krull dimensions of the ideals generated by the elements in `V` (this equals the dimension of `V(f(⋅,π⁺))` in the affine space `ℂⁿ` or its real part, given that it contains a real non-singular point).

See also: [`filter_dimension`](@ref), [`tfpv_candidates`](@ref)
"""
function print_results(idx::Vector{Vector{Bool}}, V, dim_V::Vector{Vector{Int64}}, problem::ReductionProblem)
  @assert length(idx) == length(V) "`idx` and `V` need to have the same length"
  for i in eachindex(idx)
    println("$i")
    idx_fast = idx[i]
    println(" π̃: [" * join([idx_fast[k] ? string(problem.π[k]) : "0" for k = 1:length(idx_fast)], ", ") *"]")
    V_str = [" [" * replace(join(string.(V[i][k]), ", ")) * "], $(dim_V[i][k])" for k in eachindex(V[i])]
    println(" V: " * join(V_str, "\n    ") * "\n")
  end
end


function print_system(reduction::Reduction; latex=false)
  if latex
    latexstring("f(x, \\tilde{\\pi}) = $(latexify(string.(reduction.f⁰); env=:raw)) + \\varepsilon $(latexify(string.(reduction.f¹); env=:raw))")
  else
    "f(x,π̃) = (" * join(string.(reduction.f⁰), ", ") * ") + ε(" * join(string.(reduction.f¹), ", ") * ")"
  end
end 



